dnl Process this file with autoconf to create configure.

dnl ################################################################
dnl # Initialize autoconf
dnl ################################################################
AC_INIT(libskin, 0.1.0, chipx86@chipx86.com)
AC_PREREQ(2.50)
AC_CONFIG_SRCDIR(config.h.in)
AC_COPYRIGHT([Copyright 2005-2006 Christian Hammond])


dnl ################################################################
dnl # Version information
dnl ################################################################
LIBSKIN_MAJOR_VERSION=0
LIBSKIN_MINOR_VERSION=1
LIBSKIN_MICRO_VERSION=0
LIBSKIN_DEVEL_VERSION=0

LIBSKIN_VERSION=$LIBSKIN_MAJOR_VERSION.$LIBSKIN_MINOR_VERSION.$LIBSKIN_MICRO_VERSION

if test "x$LIBSKIN_DEVEL_VERSION" != "x0"; then
	LIBSKIN_VERSION=$LIBSKIN_VERSION.$LIBSKIN_DEVEL_VERSION
fi

AC_DEFINE_UNQUOTED(LIBSKIN_MAJOR_VERSION, $LIBSKIN_MAJOR_VERSION,
	[libskin major version.])
AC_DEFINE_UNQUOTED(LIBSKIN_MINOR_VERSION, $LIBSKIN_MINOR_VERSION,
	[libskin minor version.])
AC_DEFINE_UNQUOTED(LIBSKIN_MICRO_VERSION, $LIBSKIN_MICRO_VERSION,
	[libskin micro version.])
AC_DEFINE_UNQUOTED(LIBSKIN_VERSION, "$LIBSKIN_VERSION",
	[libskin version.])

dnl ################################################################
dnl # libtool versioning
dnl ################################################################
dnl #
dnl # +1 :  0 : +1   == new interface that does not break old one.
dnl # +1 :  0 :  0   == removed an interface. Breaks old apps.
dnl #  ? : +1 :  ?   == internal changes that don't break anything.
dnl #
dnl # CURRENT : REVISION : AGE
dnl #
LT_CURRENT=2
LT_REVISION=4
LT_AGE=0

LT_RELEASE=$LIBSKIN_MAJOR_VERSION.$LIBSKIN_MINOR_VERSION.$LIBSKIN_MICRO_VERSION

AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

dnl ################################################################
dnl # Initialize automake
dnl ################################################################
VERSION=$LIBSKIN_VERSION
PACKAGE=libskin

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

dnl ################################################################
dnl # Specify a header configuration file
dnl ################################################################
AM_CONFIG_HEADER(config.h)

dnl ################################################################
dnl # Check for some standard stuff.
dnl ################################################################
AC_PROG_CC
#AC_ARG_PROGRAM
AC_PROG_CPP
AC_PROG_MAKE_SET
AC_PROG_LN_S
AC_PROG_INSTALL

AC_C_CONST
AC_TYPE_SIZE_T
AC_EXEEXT

AM_PROG_LIBTOOL

pkg_modules="pango >= 1.4.0, glib-2.0 >= 2.4.0, gtk+-2.0 >= 2.4.0, libxml-2.0 libgnomecanvas-2.0 >= 2.14.0"
PKG_CHECK_MODULES(PACKAGE, [$pkg_modules])
AC_SUBST(PACKAGE_CFLAGS)
AC_SUBST(PACKAGE_LIBS)

GLIB_GENMARSHAL=`pkg-config --variable=glib_genmarshal glib-2.0`
AC_SUBST(GLIB_GENMARSHAL)

AC_DEFINE_UNQUOTED(DBUS_API_SUBJECT_TO_CHANGE, ,DBUS API is subject to change)

dnl # Use wall if we have GCC
if test "x$GCC" = "xyes"; then
	CFLAGS="$CFLAGS -Wall"
fi

AC_SUBST(CFLAGS)

NO_UNDEFINED=""
case "$host" in
*-*-mingw*)
	NO_UNDEFINED="-no-undefined";;
esac
AC_SUBST(NO_UNDEFINED)

# define a MAINT-like variable REBUILD which is set if Perl
# is found, so autogenerated sources can be rebuilt

AC_CHECK_PROGS(PERL, perl5 perl)

REBUILD=\#
if test "x$enable_rebuilds" = "xyes" && \
	test -n "$PERL" && \
		$PERL -e 'exit !($] >= 5.002)' > /dev/null 2>&1 ; then
	REBUILD=
fi
AC_SUBST(REBUILD)


dnl ################################################################
dnl # Set up gtk-doc
dnl ################################################################
GTK_DOC_CHECK([1.4])

dnl ################################################################
dnl # Check for iso-codes
dnl ################################################################
AC_MSG_CHECKING([for iso-codes package])
have_iso_codes=no
if $PKG_CONFIG --exists iso-codes; then
	if $PKG_CONFIG iso-codes --atleast-version=0.49; then
		have_iso_codes=yes
		AC_MSG_RESULT([$have_iso_codes])
	else
		AC_MSG_WARN([iso-codes detected, but version 0.49 or later is required due to licensing])
	fi
else
	AC_MSG_RESULT([$have_iso_codes])
fi

if test "x$have_iso_codes" = "xyes"; then
	AC_MSG_CHECKING([whether iso-codes has iso-639 and iso-3166 domains])
	if $PKG_CONFIG --variable=domains iso-codes | grep -q 639 && \
		$PKG_CONFIG --variable=domains iso-codes | grep -q 3166 ; then
		result=yes
	else    
		result=no
		have_iso_codes=no
	fi
	AC_MSG_RESULT([$result])
fi

if test "x$have_iso_codes" = "xyes"; then
	AC_DEFINE_UNQUOTED([ISO_CODES_PREFIX],["`$PKG_CONFIG --variable=prefix iso-codes`"],[ISO codes prefix])
	AC_DEFINE([HAVE_ISO_CODES],[1],[Define if you have the iso-codes package])
fi

dnl ################################################################
dnl # Output the Makefiles
dnl ################################################################
AC_CONFIG_FILES([
libskin.pc
Makefile
libskin/Makefile
tests/Makefile
docs/Makefile
docs/reference/Makefile
])

AC_OUTPUT

echo
echo $PACKAGE v$VERSION

echo
echo "Now type make to compile"
echo "Then su to root and type: make install"
echo
